
!WorldState class methodsFor: 'world menu items'!
systemOn: aBuilder 
	
	<worldMenu>  
	(aBuilder item: #System)
		order: 3.0;
		icon: self theme smallConfigurationIcon;
		with: [
			(aBuilder item: #'About...') 
				order: 0;
				icon:self theme smallLanguageIcon;
				action: [Smalltalk aboutThisSystem].
			(aBuilder item: #'Software update')
				order: 2;
				icon:self theme smallUpdateIcon;
				precondition: [self showUpdateOptionInWorldMenu];
				action: [UpdateStreamer new updateFromServer ];
				help: 'Load latest code updates via the internet';
			withSeparatorAfter.
			
			(aBuilder item: #'Start profiling all Processes') 
				action: [self startMessageTally].
			(aBuilder item: #'Start profiling UI ') 
				action: [self startThenBrowseMessageTally].
			(aBuilder item: #'Space left') 
				action: [Smalltalk informSpaceLeftAfterGarbageCollection];
			withSeparatorAfter.
			
			(aBuilder item: #'Start drawing again') 
				action: [World  resumeAfterDrawError].
			(aBuilder item: #'Start stepping again') 
				action: [World  resumeAfterStepError]; 
			withSeparatorAfter.
			
			(aBuilder item: #'Restore display (r)') 
				action: [World restoreMorphicDisplay].
			Halt isHaltOnceEnabled
				ifTrue: [(aBuilder item: 'Disable halt/inspect once') action: [self clearHaltOnce]]
				ifFalse: [(aBuilder item: 'Enable halt/inspect once') action: [self setHaltOnce]]].! !
